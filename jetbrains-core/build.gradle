// Copyright 2019 Amazon.com, Inc. or its affiliates. All Rights Reserved.
// SPDX-License-Identifier: Apache-2.0

import software.aws.toolkits.telemetry.generator.gradle.GenerateTelemetry

apply plugin: 'org.jetbrains.intellij'

buildscript {
    repositories {
        mavenCentral()
        maven { setUrl("https://jitpack.io") }
    }
    dependencies {
        classpath("software.aws.toolkits:telemetry-generator:0.0.17")
    }
}

intellij {
    def rootIntelliJTask = rootProject.intellij
    version ideSdkVersion("IC")
    pluginName rootIntelliJTask.pluginName
    updateSinceUntilBuild rootIntelliJTask.updateSinceUntilBuild
    downloadSources = rootIntelliJTask.downloadSources
    plugins = idePlugins("IC")
}

patchPluginXml {
    sinceBuild ideSinceVersion()
    untilBuild ideUntilVersion()
}

configurations {
    testArtifacts
}

task generateTelemetry(type: GenerateTelemetry) {
    inputFiles = []
    outputDirectory = file("${project.buildDir}/generated-src")
}
compileKotlin.dependsOn(generateTelemetry)

sourceSets {
    main.kotlin.srcDirs += "${project.buildDir}/generated-src"
}

test {
    systemProperty("log.dir", "${org.jetbrains.intellij.Utils.systemDir(intellij.sandboxDirectory, true)}/logs")
}

task testJar (type: Jar) {
    archiveBaseName = "${project.name}-test"
    from sourceSets.test.output
    from sourceSets.integrationTest.output
}

task pluginChangeLog(type: GenerateChangeLog) {
    includeUnreleased = true
    generateGithub = false
    issuesUrl = "https://github.com/aws/aws-toolkit-jetbrains/issues"
    jetbrainsChangeNotesFile = project.file("$buildDir/changelog/change-notes.xml")
}

jar.dependsOn(pluginChangeLog)
jar {
    archiveBaseName = 'aws-intellij-toolkit-core'
    from(pluginChangeLog.jetbrainsChangeNotesFile) {
        into "META-INF"
    }
}

artifacts {
    testArtifacts testJar
}

dependencies {
    api(project(":core"))
    api("software.amazon.awssdk:s3:$awsSdkVersion")
    api("software.amazon.awssdk:lambda:$awsSdkVersion")
    api("software.amazon.awssdk:iam:$awsSdkVersion")
    api("software.amazon.awssdk:ecs:$awsSdkVersion")
    api("software.amazon.awssdk:cloudformation:$awsSdkVersion")
    api("software.amazon.awssdk:schemas:$awsSdkVersion")
    api("software.amazon.awssdk:cloudwatchlogs:$awsSdkVersion")
    api("software.amazon.awssdk:apache-client:$awsSdkVersion")
    api("software.amazon.awssdk:resourcegroupstaggingapi:$awsSdkVersion")

    testImplementation project(path: ":core", configuration: 'testArtifacts')
    testImplementation('com.github.tomakehurst:wiremock-jre8:2.26.0')

    integrationTestImplementation('org.eclipse.jetty:jetty-servlet:9.4.15.v20190215')
    integrationTestImplementation('org.eclipse.jetty:jetty-proxy:9.4.15.v20190215')
}
