// Copyright 2019 Amazon.com, Inc. or its affiliates. All Rights Reserved.
// SPDX-License-Identifier: Apache-2.0

import software.aws.toolkits.telemetry.generator.gradle.GenerateTelemetry
import toolkits.gradle.changelog.tasks.GeneratePluginChangeLog

apply plugin: 'org.jetbrains.intellij'

buildscript {
    repositories {
        mavenCentral()
        maven { setUrl("https://jitpack.io") }
    }
    dependencies {
        classpath("software.aws.toolkits:telemetry-generator:$telemetryVersion")
    }
}

intellij {
    def rootIntelliJTask = rootProject.intellij
    version ideSdkVersion("IC")
    pluginName rootIntelliJTask.pluginName
    updateSinceUntilBuild rootIntelliJTask.updateSinceUntilBuild
    downloadSources = rootIntelliJTask.downloadSources
    plugins = idePlugins("IC")
}

patchPluginXml {
    sinceBuild ideSinceVersion()
    untilBuild ideUntilVersion()
}

configurations {
    testArtifacts
}

task generateTelemetry(type: GenerateTelemetry) {
    inputFiles = []
    outputDirectory = file("${project.buildDir}/generated-src")
}
compileKotlin.dependsOn(generateTelemetry)

sourceSets {
    main.kotlin.srcDirs += "${project.buildDir}/generated-src"
}

test {
    // TODO remove, workaround for Corretto-11.0.7.10.1 mac crashing on test runs.
    jvmArgs '-XX:CompileCommand=exclude,com/intellij/openapi/progress/impl/CoreProgressManager.registerIndicatorAndRun'
    systemProperty("log.dir", "${project.intellij.sandboxDirectory}-test/logs")
}

tasks.register('pluginChangeLog', GeneratePluginChangeLog) {
    includeUnreleased = true
    changeLogFile = project.file("$buildDir/changelog/change-notes.xml")
}

jar.dependsOn(pluginChangeLog)
jar {
    archiveBaseName = 'aws-intellij-toolkit-core'
    from(pluginChangeLog.changeLogFile) {
        into "META-INF"
    }
}

dependencies {
    api(project(":core"))
    api("software.amazon.awssdk:s3:$awsSdkVersion")
    api("software.amazon.awssdk:lambda:$awsSdkVersion")
    api("software.amazon.awssdk:iam:$awsSdkVersion")
    api("software.amazon.awssdk:ecs:$awsSdkVersion")
    api("software.amazon.awssdk:cloudformation:$awsSdkVersion")
    api("software.amazon.awssdk:schemas:$awsSdkVersion")
    api("software.amazon.awssdk:cloudwatchlogs:$awsSdkVersion")
    api("software.amazon.awssdk:apache-client:$awsSdkVersion")
    api("software.amazon.awssdk:resourcegroupstaggingapi:$awsSdkVersion")
    api("software.amazon.awssdk:rds:$awsSdkVersion")
    api("software.amazon.awssdk:redshift:$awsSdkVersion")
    api("software.amazon.awssdk:secretsmanager:$awsSdkVersion")

    testImplementation project(path: ":core", configuration: 'testArtifacts')
    testImplementation('com.github.tomakehurst:wiremock-jre8:2.26.0')
    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion")
    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-debug:$coroutinesVersion")

    integrationTestImplementation('org.eclipse.jetty:jetty-servlet:9.4.15.v20190215')
    integrationTestImplementation('org.eclipse.jetty:jetty-proxy:9.4.15.v20190215')
}
