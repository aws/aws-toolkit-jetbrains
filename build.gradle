// Copyright 2019 Amazon.com, Inc. or its affiliates. All Rights Reserved.
// SPDX-License-Identifier: Apache-2.0

import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "gradle.plugin.org.jetbrains.intellij.plugins:gradle-intellij-plugin:$ideaPluginVersion"
        classpath "com.adarshr:gradle-test-logger-plugin:1.7.0"
    }
}

apply from: 'intellijJVersions.gradle'

group 'software.aws.toolkits'
version toolkitVersion

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    apply plugin: "com.adarshr.test-logger"
    apply plugin: 'jacoco'

    tasks.withType(org.jetbrains.intellij.tasks.RunIdeTask) {
        systemProperty("aws.toolkits.enableTelemetry", false)
        intellij {
            if (System.env.ALTERNATIVE_IDE) {
                alternativeIdePath = System.env.ALTERNATIVE_IDE
            }
        }
    }
}

// Kotlin plugin seems to be bugging out when there are no kotlin sources
configure(subprojects - project(":telemetry-client")) {
    apply plugin: 'kotlin'

    sourceSets {
        integrationTest {
            kotlin.srcDir 'it'
        }
    }
}

subprojects {
    group = parent.group
    version = parent.version

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'signing'

    def isReleaseVersion = !version.endsWith("SNAPSHOT")

    signing {
        required { isReleaseVersion && gradle.startParameter.taskNames.contains("publishPlugin") }
        sign configurations.archives
    }

    sourceSets {
        main.java.srcDir 'src'
        main.resources.srcDir 'resources'
        test.java.srcDir 'tst'
        test.resources.srcDir 'tst-resources'
        integrationTest {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            java.srcDir 'it'
            resources.srcDir 'it-resources'
        }
    }

    configurations {
        integrationTestImplementation.extendsFrom testImplementation
        integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
        compile "org.jetbrains.kotlin:kotlin-script-runtime:$kotlinVersion"
        testCompile "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0"
        testCompile "org.assertj:assertj-core:$assertjVersion"
        testCompile "junit:junit:$junitVersion"
        testCompile "org.hamcrest:hamcrest-all:1.3"
    }

    testlogger {
        showFullStackTraces true
    }

    test {
        jacoco {
            // don't instrument sdk, icons, ktlint, etc.
            includes = ["software.aws.toolkits.*"]
            excludes = ["software.aws.toolkits.ktlint"]
        }
    }

    idea {
        module {
            sourceDirs -= file("it")
            testSourceDirs += file("it")

            resourceDirs -= file("it-resources")
            testResourceDirs += file("it-resources")
        }
    }

    if (gradle.startParameter.taskNames.contains("check")) {
        tasks.withType(JavaCompile).all {
            options.compilerArgs << "-Werror"
        }
        tasks.withType(KotlinCompile).all {
            kotlinOptions.allWarningsAsErrors = true
        }
    }

    task integrationTest(type: Test) {
        group = LifecycleBasePlugin.VERIFICATION_GROUP
        description = "Runs the integration tests."
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath

        binResultsDir = file("$buildDir/integration-test-results/binary/integrationTest")

        reports {
            html.destination file("$buildDir/reports/integration-test")
            junitXml.destination file("$buildDir/integration-test-results")
        }

        jacoco {
            excludes = ["com.sun.*"] // See https://groups.google.com/forum/#!topic/jacoco/H0gDwxNuhK4
        }

        mustRunAfter tasks.test
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    tasks.withType(KotlinCompile).all {
        kotlinOptions.jvmTarget = "1.8"
    }

    // Force us to compile the integration tests even during check even though we don't run them
    check.dependsOn(integrationTestClasses)

    // Remove the tasks added in by gradle-intellij-plugin so that we don't publish/verify multiple times
    project.afterEvaluate {
        removeTask(tasks, 'publishPlugin')
        removeTask(tasks, 'verifyPlugin')
        if (!isReleaseVersion) {
            removeTask(tasks, "signArchives")
        }
    }
}

configurations {
    ktlint
}

def removeTask(tasks, taskName) {
    def task = tasks.findByName("$taskName")

    if (task != null) {
        task.setEnabled(false)
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion '5.4.1'
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: ChangeLogPlugin

intellij {
    version ideVersion("IC")
    pluginName 'aws-jetbrains-toolkit'
    updateSinceUntilBuild false
    downloadSources = System.getenv("CI") == null
}

publishPlugin {
    username publishUsername
    password publishPassword
    channels publishChannel
}

generateChangeLog {
    generateJetbrains = false
    generateGithub = true
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-v", project.rootDir.absolutePath + "/**/*.kt"

    inputs.files(project.fileTree(dir: "src", include: "**/*.kt"))
    outputs.dir("${project.buildDir}/reports/ktlint/")
}
check.dependsOn ktlint
check.dependsOn verifyPlugin

def publishedProjects = subprojects.findAll { !it.name.contains("test")}

task coverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    getAdditionalSourceDirs().from(publishedProjects.sourceSets.main.java.srcDirs)
    getSourceDirectories().from(publishedProjects.sourceSets.main.java.srcDirs)
    getClassDirectories().from(publishedProjects.sourceSets.main.output.classesDirs)

    reports {
        html.enabled true
        xml.enabled true
    }
}
publishedProjects.forEach {
    coverageReport.mustRunAfter(it.tasks.withType(Test))
}
check.dependsOn coverageReport

// Workaround for runIde being defined in multiple projects, if we request the root project runIde, "alias" it to
// community edition
if (gradle.startParameter.taskNames.contains("runIde")) {
    println("Top level runIde selected, excluding sub-projects' runIde")
    gradle.taskGraph.whenReady { graph ->
        graph.allTasks.forEach {
            if (gradle.startParameter.systemPropertiesArgs.getOrDefault("exec.args", "").contains("guitest")) {
                if (it.name == "runIde" && it.project != project(':jetbrains-core-gui')) {
                    it.enabled = false
                }
            } else {
                if (it.name == "runIde" && it.project != project(':jetbrains-core')) {
                    it.enabled = false
                }
            }
        }
    }
}

task guiTest(type: Test) {
    dependsOn ":jetbrains-core-gui:guiTest"
}

dependencies {
    compile project(':jetbrains-core')

    ktlint "com.pinterest:ktlint:$ktlintVersion"
    ktlint project(":ktlint-rules")
}
