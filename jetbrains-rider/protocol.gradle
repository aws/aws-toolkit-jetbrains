// Copyright 2019 Amazon.com, Inc. or its affiliates. All Rights Reserved.
// SPDX-License-Identifier: Apache-2.0

def protocolGroup = 'protocol'

ext.csGeneratedOutput = new File(resharperPluginPath, "src/AWS.Daemon/Protocol")
ext.ktGeneratedOutput = new File(projectDir, "src/software/aws/toolkits/jetbrains/protocol")

task generateModel(type: tasks.getByName("rdgen").class) {
    group = protocolGroup
    description = 'Generates protocol models'

    def modelDir = new File(projectDir, "protocol/model")

    // NOTE: classpath is evaluated lazily, at execution time, because it comes from the unzipped
    // intellij SDK, which is extracted in afterEvaluate
    params {
        verbose = true
        hashFolder = "build/rdgen"

        logger.info("Configuring rdgen params")
        classpath {
            logger.info("Calculating classpath for rdgen, intellij.ideaDependency is: ${intellij.ideaDependency}")
            def sdkPath = intellij.ideaDependency.classes
            def rdLibDirectory = new File(sdkPath, "lib/rd").canonicalFile

            "$rdLibDirectory/rider-model.jar"
        }
        sources modelDir.canonicalPath
        packages = "protocol.model"

        generator {
            language = "kotlin"
            transform = "asis"
            root = "com.jetbrains.rider.model.nova.ide.IdeRoot"
            namespace = "com.jetbrains.rider.model"
            directory = "$ktGeneratedOutput"
        }

        generator {
            language = "csharp"
            transform = "reversed"
            root = "com.jetbrains.rider.model.nova.ide.IdeRoot"
            namespace = "JetBrains.Rider.Model"
            directory = "$csGeneratedOutput"
        }
    }
}

task cleanProtocolModels {
    group = protocolGroup
    description = 'Clean up generated protocol models'

    if (csGeneratedOutput.isDirectory()) {
        csGeneratedOutput.deleteDir()
    }

    if (ktGeneratedOutput.isDirectory()) {
        ktGeneratedOutput.deleteDir()
    }
}

jar.dependsOn generateModel
