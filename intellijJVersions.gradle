static def ideProfiles() {
    return [
        "2019.2": [
            "guiTestFramework": "com.intellij.testGuiFramework:0.9.44.1@nightly",
            "sinceVersion": "192",
            "untilVersion": "192.*",
            "products" : [
                "IC": [
                    sdkVersion: "IC-2019.2",
                    plugins: [
                        "yaml",
                        "PythonCore:2019.2.192.5728.98",
                        "java",
                        "gradle",
                        "maven",
                        "properties" // Used by Maven
                    ]
                ],
                "IU": [
                    sdkVersion: "IU-2019.2",
                    plugins: [
                        "yaml",
                        "JavaScriptLanguage",
                        "JavaScriptDebugger",
                        "CSS",
                        "PythonCore:2019.2.192.5728.98"
                    ]
                ],
                "RD": [
                    sdkVersion: "RD-2019.2",
                    plugins: [
                        "yaml"
                    ]
                ]
            ]
        ],
        "2019.3": [
            "guiTestFramework": "com.intellij.testGuiFramework:193.SNAPSHOT.1@nightly",
            "sinceVersion": "193",
            "untilVersion": "193.*",
            "products": [
                "IC": [
                    sdkVersion: "IC-193.4386-EAP-CANDIDATE-SNAPSHOT", // Update to actual version when it gets released
                    plugins: [
                        "yaml",
                        "PythonCore:193.4386.10",
                        "java",
                        "gradle",
                        "maven",
                        "properties" // Used by Maven
                    ]
                ],
                "IU": [
                    sdkVersion: "IU-193.4386-EAP-CANDIDATE-SNAPSHOT",
                    plugins: [
                        "PythonCore:193.4386.10",
                        "yaml",
                        "JavaScriptLanguage",
                        "JavaScriptDebugger",
                        "CSS",
                    ]
                ],
                "RD": [
                    sdkVersion: "RD-2019.3-SNAPSHOT",
                    plugins: [
                        "yaml"
                    ]
                ]
            ]
        ]
    ]
}

def idePlugins(String productCode) {
    return ideProduct(productCode).plugins
}

def ideSdkVersion(String productCode) {
    return ideProduct(productCode).sdkVersion
}

private def ideProduct(String productCode) {
    def product = ideProfile()["products"][productCode]
    if (product == null) {
        throw new IllegalArgumentException("Unknown IDE product `$productCode` for ${resolveIdeProfileName()}")
    }
    return product
}

def ideSinceVersion() {
    def guiVersion = ideProfile()["sinceVersion"]
    if (guiVersion == null) {
        throw new IllegalArgumentException("Missing 'sinceVersion' key for ${resolveIdeProfileName()}")
    }
    return guiVersion
}

def ideUntilVersion() {
    def guiVersion = ideProfile()["untilVersion"]
    if (guiVersion == null) {
        throw new IllegalArgumentException("Missing 'untilVersion' key for ${resolveIdeProfileName()}")
    }
    return guiVersion
}

def guiTestFramework() {
    def guiVersion = ideProfile()["guiTestFramework"]
    if (guiVersion == null) {
        throw new IllegalArgumentException("Missing 'guiTestFramework' key for ${resolveIdeProfileName()}")
    }
    return guiVersion
}

def ideProfile() {
    def profileName = resolveIdeProfileName()
    def profile = ideProfiles()[profileName]
    if (profile == null) {
        throw new IllegalArgumentException("Unknown ideProfile `$profileName`")
    }

    return profile
}

def resolveIdeProfileName() {
    if (System.env.ALTERNATIVE_IDE_PROFILE_NAME) {
        return System.env.ALTERNATIVE_IDE_PROFILE_NAME
    }

    return project.ideProfileName
}

ext {
    ideProfiles = this.&ideProfiles
    idePlugins = this.&idePlugins
    ideSdkVersion = this.&ideSdkVersion
    ideSinceVersion = this.&ideSinceVersion
    ideUntilVersion = this.&ideUntilVersion
    ideProfile = this.&ideProfile
    guiTestFramework = this.&guiTestFramework
    resolveIdeProfileName = this.&resolveIdeProfileName
}
